if [[ -o interactive ]]; then
	echo 'Welcome, '`whoami`', to zsh'
fi

source $TJM_DOTFILES_PATH/zsh/plugins.zsh

#==options
setopt AUTO_CD #-# automatically change directory

#===completions
unsetopt correctall #-# auto correction of commands
unsetopt LIST_BEEP #-# disable awful errors when autocompleting
#--case insenstive
#-@ via https://stackoverflow.com/a/69014927
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|=*' 'l:|=* r:|=*'
autoload -Uz compinit && compinit

#===history
if [ -z $HISTFILE ]; then
	HISTFILE=$HOME/.zsh_history
fi
HISTSIZE=10000
SAVEHIST=10000

setopt APPEND_HISTORY #-# enable history?
setopt hist_expire_dups_first
setopt hist_ignore_space
setopt INC_APPEND_HISTORY #-# append history incrementally
setopt SHARE_HISTORY #-# share history across sessions
setopt HIST_IGNORE_ALL_DUPS

#--arrow keys match on partial history with 'zsh-history-substring-search' plugin
if typeset -f history-substring-search-up > /dev/null; then
	bindkey '^[[A' history-substring-search-up
	bindkey '^[[B' history-substring-search-down
fi

#===keys
#-@ based on https://github.com/robbyrussell/oh-my-zsh/blob/master/lib/key-bindings.zsh
#--fix delete key (forward delete)
if [[ "${terminfo[kdch1]}" != "" ]]; then
	bindkey "${terminfo[kdch1]}" delete-char
else
	bindkey "^[[3~" delete-char
	bindkey "^[3;5~" delete-char
	bindkey "\e[3~" delete-char
fi
#--word movements
bindkey '^[[1;5D' backward-word
bindkey '^[[1;5C' forward-word

#==prompt
autoload -U colors && colors
PS1="[%n:%1d]>"
tputColors="$(tput colors 2> /dev/null || echo 2)"
if [ $tputColors -gt 2 ]; then
	if [ $tputColors -gt 8 ]; then
		PS1="%{$(tput setab 42)$(tput setaf 0)%}${PS1}"
	else
		PS1="%{$bg[green]%}%{$fg[white]%}${PS1}"
	fi
	PS1+="%{$reset_color%}"
fi
unset -v tputColors
precmd(){
	echo "@ $(date '+%H:%M:%S %Y-%m-%d')"
}
preexec(){
	echo "@ $(date '+%H:%M:%S %Y-%m-%d')"
}
export PS1="$PS1 "

#==custom
if [ -f ${TJM_DOTFILES_PATH}/_local/zsh ]; then
	source ${TJM_DOTFILES_PATH}/_local/zsh
fi
